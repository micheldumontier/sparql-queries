#+ summary: get the pathways for a particular gene
#+ description: uses kg hub for gene name search, bioregistry for an http/https identifier correction, and wikipathways
#+ endpoint: https://sparql.wikipathways.org/sparql

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX bl: <https://w3id.org/biolink/vocab/>

SELECT DISTINCT ?pathway ?pathwayLabel
{
    SERVICE <http://kg-hub-rdf.berkeleybop.io/blazegraph/sparql> {
      ?gene bl:category bl:Gene ;
      rdfs:label ?geneLabel .
      FILTER(?geneLabel = "AP2B1")
    } 
    SERVICE <https://bioregistry.io/sparql> {
    	?gene owl:sameAs ?geneHttps . 
    }
    SERVICE <https://sparql.wikipathways.org/sparql> {
	?geneHttps dct:isPartOf ?pathway .
	?pathway dc:title ?pathwayLabel .
    }
}
